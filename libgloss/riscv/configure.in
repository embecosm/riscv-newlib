#=========================================================================
# configure.ac for riscv libgloss and crt0
#=========================================================================

#-------------------------------------------------------------------------
# Setup
#-------------------------------------------------------------------------

AC_INIT([crt0.S])
AC_CONFIG_SRCDIR([crt0.S])
AC_CONFIG_AUX_DIR([${srcdir}/../..])

#-------------------------------------------------------------------------
# Checks for programs
#-------------------------------------------------------------------------

LIB_AC_PROG_CC
AC_CHECK_TOOL([AR],[ar])
AC_CHECK_TOOL([RANLIB],[ranlib])
AC_PROG_INSTALL

#-------------------------------------------------------------------------
# Output
#-------------------------------------------------------------------------

AC_ARG_ENABLE(stack-erase,
[AS_HELP_STRING([--enable-stack-erase],
                [Enable stack erase checking and in longjmp])],
ENABLE_STACK_ERASE=$enableval,
ENABLE_STACK_ERASE=no)

if test "${ENABLE_STACK_ERASE}" = "yes" ; then
  AC_SUBST(STACK_ERASE, -DSTACK_ERASE)
else
  AC_SUBST(STACK_ERASE, -DNO_STACK_ERASE)
fi

if test "$srcdir" = "." ; then
  if test "${with_target_subdir}" != "." ; then
    libgloss_topdir="${srcdir}/${with_multisrctop}../../.."
  else
    libgloss_topdir="${srcdir}/${with_multisrctop}../.."
  fi
else
  libgloss_topdir="${srcdir}/../.."
fi
AC_CONFIG_AUX_DIR($libgloss_topdir)
AC_CONFIG_FILES(Makefile,
. ${libgloss_topdir}/config-ml.in,
srcdir=${srcdir}
target=${target}
with_multisubdir=${with_multisubdir}
ac_configure_args="${ac_configure_args} --enable-multilib"
CONFIG_SHELL=${CONFIG_SHELL-/bin/sh}
libgloss_topdir=${libgloss_topdir}
)
AC_OUTPUT
